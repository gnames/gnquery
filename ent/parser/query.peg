package parser

type engine Peg {
}

Query <- _? Components Tail END

Tail <- (_ .*)?

Components <- Element (_ Element)*

Element <- (TagAllResults Bool / TagString String / TagDS Number /
  TagYear (YearRange / YearNum) / TagWord Word)

TagAllResults <- AllResults ':'

TagString <- NameString ':'

TagDS <- DataSource  ':'

TagYear <- Year ':'

TagWord <- (Genus / ParentTaxon / SpeciesAny / Species /
  SpeciesInfra / Author) ':'

Author <- 'au' / 'a'

DataSource <- 'ds'

AllResults <- 'all'

Genus <- 'gen' / 'g'

NameString <- 'n'

ParentTaxon <- 'tx'

Species <- 'sp'

SpeciesAny <- 'sp+'

SpeciesInfra <- 'isp'

Year <- 'yr' / 'y'

String <- Word (_ Word)*

YearRange <- '-' YearNum / YearNum '-' YearNum?

YearNum <- [12] [0789] Digits Digits

Number <- [0-9]+

Bool <- 'true' / 't' / 'false' / 'f'

Digits <- [0-9]

Word <- [^: ]+ &(_ / END)

_ <- MultipleSpace / SingleSpace

MultipleSpace <- SingleSpace SingleSpace+

SingleSpace <- ' '

END <- !.
