package parser

type engine Peg {
}

Query <- _? Components Tail END

Tail <- (_ .*)?

Components <- Element (_ Element)*

Element <- TagString String / TagDS Numbers /
  TagYear (YearRange / YearNum) / TagAllResults Bool / TagWord Word

TagAllResults <- AllResults ':'

TagString <- NameString ':'

TagDS <- DataSources  ':'

TagYear <- Year ':'

TagWord <- (Author / Genus / ParentTaxon / SpeciesAny / Species /
  SpeciesInfra) ':'

Author <- 'au'

DataSources <- 'ds'

AllResults <- 'all'

Genus <- 'gen' / 'g'

NameString <- 'n'

ParentTaxon <- 'tx'

Species <- 'sp'

SpeciesAny <- 'asp'

SpeciesInfra <- 'isp'

Year <- 'yr' / 'y'

String <- Word (_ Word)*

YearRange <- '-' YearNum / YearNum '-' YearNum?

YearNum <- [12] [0789] Digits Digits

Numbers <- Number (',' Number)*

Number <- [0-9]+

Bool <- 'true' / 't' / 'false' / 'f'

Digits <- [0-9]

Word <- [^: ]+ &(_ / END)

_ <- MultipleSpace / SingleSpace

MultipleSpace <- SingleSpace SingleSpace+

SingleSpace <- ' '

END <- !.
